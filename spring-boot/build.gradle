buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.5.17.RELEASE")
    }
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'

ext {
    springBootVersion = '1.5.17.RELEASE'
}

jar {
    baseName = 'pullreports-spring-boot'
}

repositories {
    mavenCentral()
    maven {
        url 's3://repo.pullreports.com/maven2'
        credentials(AwsCredentials) {
            accessKey project.getProperty('accessKey')
            secretKey project.getProperty('secretKey')
        }
    }
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

configurations {
    all*.exclude group:'org.hibernate', module:'hibernate-validator'
}

dependencies {
    compile "org.springframework.boot:spring-boot-starter-web:$springBootVersion"
    compile "org.springframework.boot:spring-boot-devtools:$springBootVersion"
    compile 'com.h2database:h2:1.4.195'
    runtime "org.springframework.boot:spring-boot-starter-freemarker:$springBootVersion"
    runtime 'com.pullreports:pullreports:+'
    runtime 'org.apache.tomcat:tomcat-dbcp:8.5.24'
}

sourceSets {
    main {
        resources {
            // Include the root projects resource directory to pick
            // up the license and report configuration.
            srcDir "${project(':').projectDir}/src/main/resources"
        }
    }
}

bootRun {
	addResources = true
}

clean.doFirst {
	delete "${projectDir}/bin"
}

project.afterEvaluate {
    tasks.bootRun.dependsOn ':database:startH2'
}
