buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:2.1.11.RELEASE")
    }
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management' 


jar {
    baseName = 'pullreports-spring-boot'
}

repositories {
    mavenCentral()
    maven {
        url 's3://repo.pullreports.com/maven2'
        credentials(AwsCredentials) {
            accessKey project.getProperty('accessKey')
            secretKey project.getProperty('secretKey')
        }
    }
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

configurations {
    all*.exclude group:'org.hibernate', module:'hibernate-validator'
}

dependencies {
    compile "org.springframework.boot:spring-boot-starter-web"
    compile "org.springframework.boot:spring-boot-devtools"
    compile 'com.h2database:h2:1.4.200'
    runtime "org.springframework.boot:spring-boot-starter-freemarker"
    runtime 'com.pullreports:pullreports:+'
    runtime 'org.apache.tomcat:tomcat-dbcp:9.0.29'
    testCompile project(path:':common-test',configuration:'default')
}

sourceSets {
    main {
        resources {
            // Include the root projects resource directory to pick
            // up the license and report configuration.
            srcDir "${project(':').projectDir}/src/main/resources"
        }
    }
}

clean.doFirst {
	delete "${projectDir}/bin"
}

project.afterEvaluate {
    tasks.bootRun.dependsOn ':database:startH2'
}
