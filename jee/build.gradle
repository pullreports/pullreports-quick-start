buildscript {
    repositories {
        jcenter()
    }
}

plugins {
    id 'war'
    id 'eclipse'
    id 'project-report'
    id 'org.gretty' version '3.0.4'
}

repositories {
    jcenter()
    mavenCentral()
    maven {
        url "s3://repo.pullreports.com/maven2"
        credentials(AwsCredentials) {
            accessKey project.getProperty('accessKey')
            secretKey project.getProperty('secretKey')
        }
        content {
            includeGroupByRegex "com\\.pullreports.*"
        }
    }
}

dependencies {
    runtimeOnly 'org.slf4j:slf4j-simple:1.7.30' // SLF4J implementation
    runtimeOnly 'opensymphony:sitemesh:2.4.2' // Optional: demonstrates the use of SiteMesh and Pull Reports
    runtimeOnly 'org.apache.taglibs:taglibs-standard-impl:1.2.5'
    runtimeOnly 'com.pullreports:pullreports:+'

	// Provided by Servlet Container. 'providedCompile' configuration
    // from WAR plugin.
    providedCompile 'javax.servlet:javax.servlet-api:3.0.1'
    providedCompile 'javax.servlet.jsp:javax.servlet.jsp-api:2.2.1'
	
	// Driver for the H2 database. Must be on the Servlet Container classpath.
    gretty 'com.h2database:h2:1.4.200'
    gretty 'commons-dbcp:commons-dbcp:1.4'

    testImplementation project(path:':common-test',configuration:'default')
}

sourceSets {
    main {
        resources {
            // Include the root projects resource directory to pick
            // up the license and report configuration.
            srcDir "${project(':').projectDir}/src/main/resources"
        }
    }
}

gretty {
    servletContainer 'jetty9.3'
    contextPath ''
}

war {
    archiveName "ROOT.war"
}

clean.doFirst {
	delete "${projectDir}/bin"
	delete "${projectDir}/tomcat.8080"
}

project.afterEvaluate {
    tasks.appRun.dependsOn ':database:startH2'
}
