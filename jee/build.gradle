buildscript {
    repositories {
        jcenter()
    }
}

plugins {
    id 'war'
    id 'eclipse'
    id 'project-report'
    id 'org.gretty' version '2.3.1'
}

repositories {
    jcenter()
    mavenCentral()
    maven {
        url "s3://repo.pullreports.com/maven2"
        credentials(AwsCredentials) {
            accessKey project.getProperty('accessKey')
            secretKey project.getProperty('secretKey')
        }
    }
}

dependencies {
    runtime 'org.slf4j:slf4j-simple:1.7.29' // SLF4J implementation
    runtime 'opensymphony:sitemesh:2.4.2' // Optional: demonstrates the use of SiteMesh and Pull Reports
    runtime 'org.apache.taglibs:taglibs-standard-impl:1.2.5'
    runtime "com.pullreports:pullreports:+"

	// Provided by Servlet Container
    providedCompile 'javax.servlet:javax.servlet-api:3.0.1'
    providedCompile 'javax.servlet.jsp:javax.servlet.jsp-api:2.2.1'
	
	// Driver for the H2 database. Must be on the Servlet Container classath.
    gretty 'com.h2database:h2:1.4.200'
	
    // Ref: https://github.com/akhikhl/gretty/issues/234
    gretty 'org.apache.tomcat:tomcat-dbcp:8.0.23'
}

sourceSets {
    main {
        resources {
            // Include the root projects resource directory to pick
            // up the license and report configuration.
            srcDir "${project(':').projectDir}/src/main/resources"
        }
    }
}

gretty {
    servletContainer 'tomcat8'
    enableNaming true
    contextPath ''
}

war {
    archiveName "ROOT.war"
}

clean.doFirst {
	delete "${projectDir}/bin"
	delete "${projectDir}/tomcat.8080"
}

project.afterEvaluate {
    tasks.appRun.dependsOn ':database:startH2'
}
