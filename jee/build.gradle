plugins {
    id 'war'
    id 'eclipse'
    id 'project-report'
    id 'org.gretty' version '4.1.5'
}

dependencies {
    runtimeOnly 'org.slf4j:slf4j-simple:2.0.16' // SLF4J implementation
    implementation 'org.sitemesh:sitemesh:3.2.1' // Optional: demonstrates the use of SiteMesh and Pull Reports
    implementation project(':common')

    // Provided by Servlet Container. 'providedCompile' configuration
    // from WAR plugin.
    providedCompile 'jakarta.servlet:jakarta.servlet-api:6.0.0'

	// Driver for the H2 database. Must be on the Servlet Container classpath.
    grettyRunnerJetty11 libs.h2.driver
    grettyRunnerJetty11 'commons-dbcp:commons-dbcp:1.4'

    testImplementation project(path:':common-test',configuration:'default')
}

sourceSets {
    main {
        resources {
            // Include the root projects resource directory to pick
            // up the license and report configuration.
            srcDirs += "${project(':').projectDir}/src/main/resources"
        }
    }
}

gretty {
    servletContainer 'jetty11'
    contextPath ''
}

war {
    archiveFileName="ROOT.war"
}

project.afterEvaluate {
    tasks.appRun.dependsOn ':common:startH2'
}
