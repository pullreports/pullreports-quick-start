plugins {
    id 'war'
    id 'eclipse'
    id 'project-report'
    id 'org.gretty' version '3.1.4'
}

dependencies {
    runtimeOnly 'org.slf4j:slf4j-simple:1.7.30' // SLF4J implementation
    runtimeOnly 'opensymphony:sitemesh:2.4.2' // Optional: demonstrates the use of SiteMesh and Pull Reports
    runtimeOnly 'org.apache.taglibs:taglibs-standard-impl:1.2.5'
    implementation project(':common')

    // Provided by Servlet Container. 'providedCompile' configuration
    // from WAR plugin.
    providedCompile 'jakarta.servlet:jakarta.servlet-api:4.0.4'
    providedCompile 'javax.servlet.jsp:javax.servlet.jsp-api:2.2.1'

	// Driver for the H2 database. Must be on the Servlet Container classpath.
    grettyRunnerJetty10 libs.h2.driver
    grettyRunnerJetty10 'commons-dbcp:commons-dbcp:1.4'

    testImplementation project(path:':common-test',configuration:'default')
}

sourceSets {
    main {
        resources {
            // Include the root projects resource directory to pick
            // up the license and report configuration.
            srcDir "${project(':').projectDir}/src/main/resources"
        }
    }
}

gretty {
    servletContainer 'jetty10'
    contextPath ''
}

war {
    archiveFileName="ROOT.war"
}

project.afterEvaluate {
    tasks.appRun.dependsOn ':common:startH2'
}
